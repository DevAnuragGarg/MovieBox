// Instrumentation is a set of control methods, or hooks, in the Android system.
// These hooks control an Android component independently of the component's normal
// lifecycle. They also control how Android loads apps. Using instrumentation makes
// it possible for tests to invoke methods in the app, and modify and examine fields
// in the app, independently of the app's normal lifecycle.
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics' // Apply the Crashlytics Gradle plugin

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "com.intact.moviesbox"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.appVersionCode
        versionName rootProject.appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
        signingConfig signingConfigs.debug
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            multiDexEnabled = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    // lint options

    // view binding and data binding jetpack || ui || foundation
    buildFeatures{
        dataBinding = true
        viewBinding = true
    }
    namespace 'com.intact.moviesbox'
    lint {
        abortOnError false
        checkOnly 'NewApi', 'InlinedApi'
        disable 'TypographyFractions', 'TypographyQuotes'
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        ignoreWarnings true
        quiet true
    }
}

dependencies {
    // we could have used only the presentation layer through which app module can
    // interact, but to fill the dependencies using the dagger2, all the implementations
    // are written in the di folder of the app module.
    implementation project(path: ':data')
    implementation project(path: ':local')
    implementation project(path: ':domain')
    implementation project(path: ':remote')
    implementation project(path: ':presentation')

    // kotlin kx
    implementation "androidx.core:core-ktx:$rootProject.ktxVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"

    // rx android
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroid"

    // ReactiveStreams support for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$rootProject.lifecycleReactiveStreamVersion"

    // lifecycle
    implementation "android.arch.lifecycle:viewmodel:$rootProject.aacLifecycle"
    implementation "android.arch.lifecycle:extensions:$rootProject.aacLifecycle"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:$rootProject.rxjavaAdapter"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttpIntercepterVersion"

    // paging library
    implementation "androidx.paging:paging-runtime:$rootProject.paging"

    // picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    // room dependencies
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-rxjava2:$room"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "androidx.room:room-compiler:$room"

    // dagger
    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger"
    implementation "com.google.dagger:dagger:$rootProject.dagger"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger"
    implementation "com.google.dagger:dagger-android-support:$rootProject.dagger"

    // app center
    implementation "com.microsoft.appcenter:appcenter-crashes:$rootProject.appCenterVersion"
    implementation "com.microsoft.appcenter:appcenter-analytics:$rootProject.appCenterVersion"

    // worker
    implementation "androidx.work:work-runtime-ktx:$rootProject.workManager"

    // helper files
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"
    implementation "androidx.cardview:cardview:$rootProject.cardViewVersion"
    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlinVersion"
    implementation "androidx.recyclerview:recyclerview:$rootProject.recyclerviewVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"

    // material theme and components
    implementation "com.google.android.material:material:$rootProject.materialVersion"

    // firebase
    implementation "com.google.firebase:firebase-core:$rootProject.firebaseCore"
    implementation "com.google.firebase:firebase-analytics:$rootProject.analytics"
    implementation "com.google.firebase:firebase-crashlytics:$rootProject.crashlytics"

    // google play
    implementation "com.google.android.play:core:$rootProject.googlePlayVersion"

    // Testing dependencies
    // top three libraries are added to remove the boilerplate code
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:truth:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "org.mockito:mockito-inline:$rootProject.mockitoInlineVersion"
    androidTestImplementation "androidx.test:runner:$rootProject.testRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"
}
apply plugin: 'com.google.gms.google-services'

// crashlytics gradle plugin
apply plugin: 'com.google.firebase.crashlytics'
